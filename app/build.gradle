plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17

    }
    defaultConfig {
        namespace "com.example.mana"
        minSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withReader('UTF-8') {
            localProperties.load(it)
        }
    }

    def kakaoTokenDebug = localProperties.getProperty('kakao.login.debug.token', '')
    def kakaoTokenRelease = localProperties.getProperty('kakao.login.release.token', '')
    def naverClientIdDebug = localProperties.getProperty('naver.login.debug.client.id', '')
    def naverClientIdRelease = localProperties.getProperty('naver.login.release.client.id', '')
    def naverClientSecretKeyDebug = localProperties.getProperty('naver.login.debug.client.secretKey', '')
    def naverClientSecretKeyRelease = localProperties.getProperty('naver.login.release.client.secretKey', '')
    def googleClientIdDebug = localProperties.getProperty('google.login.debug.client.id', '')
    def googleClientIdRelease = localProperties.getProperty('google.login.release.client.id', '')

    buildTypes {
        debug {
            buildConfigField 'String', 'KAKAO_TOKEN', "\"${kakaoTokenDebug}\""
            buildConfigField 'String', 'NAVER_CLIENT_ID', "\"${naverClientIdDebug}\""
            buildConfigField 'String', 'NAVER_CLIENT_SECRET_KEY', "\"${naverClientSecretKeyDebug}\""
            buildConfigField 'String', 'GOOGLE_CLIENT_ID', "\"${googleClientIdDebug}\""
            manifestPlaceholders = [
                    kakaoLoginToken : kakaoTokenDebug,
                    analyticsEnabled: "false"
            ]
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'KAKAO_TOKEN', "\"${kakaoTokenRelease}\""
            buildConfigField 'String', 'NAVER_CLIENT_ID', "\"${naverClientIdRelease}\""
            buildConfigField 'String', 'NAVER_CLIENT_SECRET_KEY', "\"${naverClientSecretKeyRelease}\""
            buildConfigField 'String', 'GOOGLE_CLIENT_ID', "\"${googleClientIdRelease}\""
            manifestPlaceholders = [
                    kakaoLoginToken : kakaoTokenRelease,
                    analyticsEnabled: "true"
            ]
        }
    }
    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    def androidxCoreKtxVersion = "1.15.0"
    def androidxCameraxVersion = "1.4.2"
    def androidxAppCompatVersion = "1.7.0"
    def androidxConstraintLayoutVersion = "2.2.1"
    def androidxRecyclerViewVersion = "1.4.0"
    def androidxRecyclerSelectionViewVersion = "1.1.0"
    def androidxJunitVersion = "1.2.1"
    def androidxEspressoVersion = "3.6.1"
    def androidxCredentialsVersion = "1.5.0"
    def googleFirebaseMessagingVersion = "24.1.1"
    def googleAndroidMaterialVersion = "1.12.0"
    def googlePlayServicesLocationVersion = "21.3.0"
    def googleAndroidIdentityGoogleIdVersion = "1.1.1"
    def glideVersion = "4.12.0"
    def bootpayVersion = "4.9.0"
    def bootpayBioVersion = "4.4.21"
    def kakaoUserVersion = "2.21.1"
    def timberVersion = "5.0.1"
    def naverOauthVersion = "5.10.0"
    def naverMapVersion = "3.21.0"
    def junitVersion = "4.13.2"
    def webrtcVersion = "125.6422.07"

    implementation "androidx.core:core-ktx:${androidxCoreKtxVersion}"
    implementation "androidx.appcompat:appcompat:${androidxAppCompatVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${androidxConstraintLayoutVersion}"
    implementation "androidx.recyclerview:recyclerview:${androidxRecyclerViewVersion}"
    implementation "androidx.recyclerview:recyclerview-selection:${androidxRecyclerSelectionViewVersion}"
    implementation "androidx.camera:camera-core:${androidxCameraxVersion}"
    implementation "androidx.camera:camera-camera2:${androidxCameraxVersion}"
    implementation "androidx.camera:camera-lifecycle:${androidxCameraxVersion}"
    implementation "androidx.camera:camera-video:${androidxCameraxVersion}"
    implementation "androidx.camera:camera-view:${androidxCameraxVersion}"
    implementation "androidx.camera:camera-extensions:${androidxCameraxVersion}"
    implementation "com.google.firebase:firebase-messaging:${googleFirebaseMessagingVersion}"
    implementation "com.google.android.material:material:${googleAndroidMaterialVersion}"
    implementation "com.google.android.gms:play-services-location:${googlePlayServicesLocationVersion}"
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    annotationProcessor "com.github.bumptech.glide:compiler:${glideVersion}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${glideVersion}"
    implementation "io.github.bootpay:android:${bootpayVersion}"
    implementation "io.github.bootpay:android-bio:${bootpayBioVersion}"
    implementation "com.kakao.sdk:v2-user:${kakaoUserVersion}"
    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation "com.navercorp.nid:oauth:${naverOauthVersion}"
    implementation "com.naver.maps:map-sdk:${naverMapVersion}"
    implementation "io.github.webrtc-sdk:android:${webrtcVersion}"
    implementation "androidx.credentials:credentials:${androidxCredentialsVersion}"
    implementation "androidx.credentials:credentials-play-services-auth:${androidxCredentialsVersion}"
    implementation "com.google.android.libraries.identity.googleid:googleid:${googleAndroidIdentityGoogleIdVersion}"
    testImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "androidx.test.ext:junit:${androidxJunitVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${androidxEspressoVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}
